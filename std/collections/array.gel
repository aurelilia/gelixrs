import std/iter/+
import std/memory/+
import std/ops/+

// A simple, variable-length array type.
// E is the type of values it can hold.
//
// To access array items, use the index operator: array[index]
// Accessing an item above the index will cause a crash.
//
// To create a new array, use a literal:
// val arr = [0, 1, 2, 3]
// val text = ["Hello", "I'm", "A", "String", "Array!"]
//
// This is not valid since the type of elements cannot be inferred:
// val a = []
// If you need an empty array, use the constructor on the class.
//
// Retrieving elements works as you'd expect using indexing syntax:
// print([2, 123, 312][1]) // prints 123
class Array<E> {

    var len: usize
    var capacity: usize
    var ptr: *E

    /// Construct an empty array with the given capacity.
    /// Will allocate the amount given on the heap (malloc),
    /// capacity of 0 results in no allocation until first insertion.
    construct(capacity) {
        this.ptr = allocate::<E>(capacity)
        this.len = 0
    }

    /// Simply construct an array out of raw parts.
    /// Rarely useful, unless dealing with low-level code.
    construct(len, capacity, ptr)

    func get(index: usize) -> E {
        if (index >= this.len) panic("Array index out of bounds!")
        load_value_index(this.ptr, index)
    }

    func set(index: usize, element: E) -> E {
        if (index >= this.len) panic("Array index out of bounds!")

        val old_elem = this.get(index)
        dec_ref(old_elem)

        inc_ref(element)
        write_value_index(this.ptr, index, element)

        old_elem
    }

    func push(elem: E) {
        this.maybe_extend()
        inc_ref(elem)

        // This sets the pointer directly, as Array.set will
        // return the old element - which would be unallocated memory,
        // resulting in a segfault.
        write_value_index(this.ptr, this.len, elem)
        this.len = this.len + 1
    }

    func pop() -> Opt<E> {
        if (this.len == 0) return Opt::<E>:None

        this.len = this.len - 1
        val value = this.get(this.len)

        dec_ref(value)
        Opt:Some(value)
    }

    func maybe_extend() {
        if (this.len == this.capacity) this.extend()
    }

    func extend() {
        this.capacity = (this.capacity + 1) * 2
        val new_ptr = allocate::<E>(this.capacity)
        copy_ptr(this.ptr, new_ptr, this.len)
        drop(this.ptr)
        this.ptr = new_ptr
    }
}

impl Free for &Array<E> {
    func free() {
        for (elem from 0 to this.len) {
            dec_ref(this[elem])
        }
        drop(this.ptr)
    }
}

impl IndexGet<usize, E> for &Array<E> {
    func index_get(index: usize) -> E {
        this.get(index)
    }
}

impl IndexSet<usize, E> for &Array<E> {
    func index_set(index: usize, value: E) {
        this.set(index, value)
    }
}

class ArrayIter<E> {
    val arr: Array<E>
    var index = 0

    construct(arr)
}

impl Iter<E> for ArrayIter<E> {
    func next() -> Opt<E> {
        if (this.arr.len == this.index) Opt::<E>:None
        else {
            this.index = this.index + 1
            Opt:Some(this.arr[this.index - 1])
        }
    }
}

impl ToIter<E, ArrayIter<E>> for Array<E> {
    func iter() -> ArrayIter<E> = ArrayIter(this)
}
